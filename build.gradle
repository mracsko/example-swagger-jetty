plugins {
    id "org.hidetake.swagger.generator" version "2.9.0"
    id "java"
    id "application"
    //id "com.dorongold.task-tree" version "1.3"
}

group 'com.mracsko.examples'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    checkerFrameworkAnnotatedJDK {
        description = 'a copy of JDK classes with Checker Framework type qualifers inserted'
    }
}

dependencies {
    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.8.v20171121'
    compile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.4.8.v20171121'

    compile group: 'org.glassfish.jersey.core', name: 'jersey-server', version: '2.26'
    compile group: 'org.glassfish.jersey.media', name: 'jersey-media-multipart', version: '2.26'
    compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-jetty-servlet', version: '2.26'
    compile group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version: '2.26'

    compile group: 'com.fasterxml.jackson.jaxrs', name: 'jackson-jaxrs-json-provider', version: '2.9.4'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.9.4'

    compile group: 'io.swagger', name: 'swagger-jersey2-jaxrs', version: '1.5.18'

    compile group: 'com.google.inject', name: 'guice', version: '4.1.0'
    compile group: 'com.google.inject.extensions', name: 'guice-servlet', version: '4.1.0'
    compile group: 'com.google.guava', name: 'guava', version: '24.0-jre'

    compile group: 'org.webjars', name: 'swagger-ui', version: '3.9.3'

    compile group: 'javax.activation', name: 'activation', version: '1.1.1'

    compile group: 'org.checkerframework', name: 'checker-qual', version: '2.3.2'

    runtime group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'

    testRuntime group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'

    testCompile group: 'junit', name: 'junit', version: '4.12'

    compileOnly group: 'org.checkerframework', name: 'checker', version: '2.3.2'
    testCompileOnly group: 'org.checkerframework', name: 'checker', version: '2.3.2'
    checkerFrameworkAnnotatedJDK group: 'org.checkerframework', name: 'jdk8', version: '2.3.2'

    swaggerCodegen 'io.swagger:swagger-codegen-cli:2.3.1'
}

swaggerSources {
    dummy {
        inputFile = file('src/main/swagger/swagger.yaml')
        code {
            language = 'jaxrs'
            additionalProperties = [
                    "apiPackage"  : "com.mracsko.example.rest.swagger.api",
                    "modelPackage": "com.mracsko.example.rest.swagger.model",
                    "dateLibrary" : "java8"]
            dependsOn validation
            templateDir = file('src/main/swagger/template/JavaJaxRS')
        }
    }
}

compileJava.dependsOn swaggerSources.dummy.code
sourceSets.main.java.srcDir "${swaggerSources.dummy.code.outputDir}/src/gen/java"

allprojects {
    tasks.withType(JavaCompile).all { JavaCompile compile ->
        compile.doFirst {
            compile.options.compilerArgs = [
                    '-processor', 'org.checkerframework.checker.nullness.NullnessChecker',
                    "-Xbootclasspath/p:${configurations.checkerFrameworkAnnotatedJDK.asPath}",
            ]
        }
    }
}

mainClassName = "com.mracsko.example.rest.swagger.guice.jersey.Main"
